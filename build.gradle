buildscript {
	repositories {
		mavenCentral()
		maven {
			name = "forge"
			url = "https://files.minecraftforge.net/maven"
		}
		maven {
			name = "github"
			url = "https://github.com/juanmuscaria/maven/raw/master"
		}
	}
	dependencies {
		classpath 'net.minecraftforge.gradle:ForgeGradle:1.2-1.5.6-SNAPSHOT'
		classpath 'com.integral.grimoire:GrimoireShenanigans:[1.5.3,)'
	}
}

apply plugin: 'scala'
apply plugin: 'forge'
apply plugin: 'grimoire-shenanigans'

minecraft {
	version = "1.7.10-10.13.4.1614-1.7.10"
	runDir = "run"
	replace("@version@", version)
}

sourceCompatibility = targetCompatibility = compileJava.sourceCompatibility = compileJava.targetCompatibility = '1.8'
tasks.withType(JavaCompile).configureEach {
	options.encoding = "UTF-8"
}

compileScala.sourceCompatibility = compileScala.targetCompatibility = '1.8'
tasks.withType(ScalaCompile).configureEach {
	options.encoding = "UTF-8"
}

repositories {
	mavenCentral()
	maven {
		name = 'spongepowered-repo'
		url = "https://repo.spongepowered.org/maven"
	}
	maven {
		url = "https://jitpack.io"
	}
	flatDir {
		dirs 'libs'
	}
}

dependencies {
	compile group: "org.spongepowered", name: "mixin", version: "0.7.11-SNAPSHOT"
	annotationProcessor 'org.spongepowered:mixin:0.7.11-SNAPSHOT:processor'

	compile fileTree(dir: 'libs', include: ['*.jar'])
}

processResources {
	from(sourceSets.main.resources.srcDirs) {
		include 'mcmod.info'
		expand 'version': version, 'mcversion': project.minecraft.version
	}
	from(sourceSets.main.resources.srcDirs) {
		include 'application.conf'
		filter { line ->
			line.replaceAll("@version@", "${version}")
		}
	}
	from(sourceSets.main.resources.srcDirs) {
		exclude 'mcmod.info'
		exclude 'application.conf'
	}
}

tasks.register('clientJar', Jar) {
	from sourceSets.main.scala
	from sourceSets.main.java
	archiveClassifier.set 'client'
	exclude "com/suslovila/server/**"
	exclude 'META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'module-info.class'
}

tasks.register('serverJar', Jar) {
	from sourceSets.main.scala
	from sourceSets.main.java
	archiveClassifier.set 'server'
	exclude "com/suslovila/client/**"
	exclude "assets/**"
	exclude 'META-INF/INDEX.LIST', 'META-INF/*.SF', 'META-INF/*.DSA', 'META-INF/*.RSA', 'module-info.class'
}

build.dependsOn clientJar, serverJar
